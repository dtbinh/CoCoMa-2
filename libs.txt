;;; File to be included in NetLogo Mutliagent Models
;;; BDI Architecture for NetLogo Models
;;; Includes belief revision procedures and Intentions Handling and Execution.
;;; Original Version for Netlogo 2 (2005) I. Sakellariou
;;; Adapted to NetLogo 4 (2008) I. Sakellariou

;;; Requirements
;;; 1) All agents that modeled as "BDI" agent have two declared -own variables beliefs intentions.
;;; These are the variables to which all beliefs and intentions are recorded. So, in your model if there is a breed of turtles 
;;; which you desire to model as BDI, then you should have a BREED-own [beliefs intentions] declaration (along with any other 
;;; variables that you wish to include in your model. 
;;; MAKE SURE that when you create the variables you set their initial values to empty list ([]).
;;; 2) YOU also must have ticks!! in your model (or timeout will not work).
;;; 3) Your model should have a switch (check NetLogo manual) named "show-intentions" 

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;; BELIEFS
;; There is nothing magical about beliefs: they are simply items 
;; of a specific list. Check list processing in NetLogo. However
;; here are some usefull procedures. 

;;; creates a new belief. (does not stores it in belief memory).
to-report create-belief [b-type content]
  report (list b-type content)
end 

;;; reports type of a belief.
to-report belief-type [bel]
  report first bel
end

;; reports the coontent of belief belief 
to-report belief-content [bel]
  report item 1 bel
end

;; Adding information to the beliefs structure
to add-belief [bel]
  if member? bel beliefs [stop]
  set beliefs fput bel beliefs 
end 
;; Removing a belief from the list of beliefs. 
to remove-belief [bel]
 set beliefs remove bel beliefs 
end 
 
;;; return true if a specific belief belong to the set of beliefs
to-report exists-belief [bel]
   ifelse member? bel beliefs [report true] [report false]
end

;;; Reports true if a belief in the form of ["b-type" etc etc etc ...] exist in beliefs list
to-report exist-beliefs-of-type [b-type]
  let blfs filter [first ? = b-type] beliefs
  ifelse empty? blfs [report false] [report true]
end

;;; Returns all beliefs of b-type in a list
to-report beliefs-of-type [b-type]
  report filter [first ? = b-type] beliefs
end

;;; Returns the first belief of a certain type and removes it
to-report get-belief [b-type]
  ifelse exist-beliefs-of-type b-type 
  [let bel first filter [first ? = b-type] beliefs
   remove-belief bel
   report bel
  ]
  [report false]
end

to-report read-first-belief-of-type [b-type]
   report first beliefs-of-type b-type
end 

to update-belief [bel]
   remove-belief read-first-belief-of-type belief-type bel
   add-belief bel
end


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;; CODE FOR HANDLING INTENTIONS-A simple deliberative architecture
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

to execute-intentions
;;;locals [myInt]  ;; first intentions
  if empty? intentions [stop]
  let myInt get-intention
  run intention-name myInt
  if runresult intention-done myInt [remove-intention myInt]
  if show-intentions [set label intentions] ;; Just for debugging. 
end 

;;;; Intentions Structure Access Functions
;; returns the current intention of the agent
to-report current-intention
  report intention-name first intentions  
end 

;; Reports the full intention structure 
to-report get-intention
  report first intentions
end 

;; Returns the intetnion name (the executable)
to-report intention-name [intention]
  report item 0 intention
end

;; return the done-methods (arguments) of the intention. If it evaluates to true 
;; then the intention is removed. 
to-report intention-done [intention]
  report item 1 intention
end 

to pop-intention 
  set intentions but-first intentions
end

;; Removes a specific intention from the intention stack 
to remove-intention [bdi-lib##intention]
  set intentions remove-item (position bdi-lib##intention intentions) intentions
end 



;; Adds an intention in the intentions list. REMEMBER that intentions are 
;; stored in a STACK!
;; The first argument is the intention name that should be some executable procedure 
;; you encode in NetLogo. The second argument should be a REPORTER that when evaluates to 
;; true the intention is removed (either accomplished or dropped). 
;; BOTH ARGUMENTS HAVE TO BE STRINGS (see run/runresult primitive procedures in NetLogo)

to add-intention [name done]
  set intentions fput (list name done) intentions
end 
 
;;;; SPECIAL ACTIONS 
;;; a null action
to do-nothing
end 

;;; wait for something until the timeout expires.
to wait-for-timeout 
  do-nothing
end 

;;;
to-report timeout_expired [timeout]
report (word "timeout_has_expired " ticks " " timeout) 
end

;;; INTERNAL not to be USED. 
;;; reports the end of the timeout. 
to-report timeout_has_expired [start interval]
  report (start + interval < ticks )
end



;;; File to be included in NetLogo Mutliagent Models
;;; Communication for NetLogo Multiagent models
;;; Includes primitives for message creation and handling in NetLogo 
;;; Original Version for Netlogo 2 (2005) I. Sakellariou
;;; Adapted to NetLogo 4 (2008) I. Sakellariou

;;; Requirements
;;; All agents that are able to communicate MUST have a declated -own variable incoming-queue.
;;; This is the variable to which all messages are recorded. So, in your model if there is a breed of turtles 
;;; which you desire to communicate, then you should have a BREED-own [incoming-queue] declaration (along with any other 
;;; variables that you wish to include in your model.
;;; MAKE SURE that when you create the variables you set its values to empty list ([]).




;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;; COMMUNICATION
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;; MESSAGE PROCESSING ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Sending Messages
;; (One man's send is another man's receive..)
;; The second commented out line presents an alternative send implementation. 
;; The commented out line represents an alternative method. 
;; Problem: What if the agent I am sending the message is "killed" 
;; Solution: Nothing Happens. Could yield an error message Alternative: create a safe send.
to send [msg]
  let recipients get-receivers msg
  let recv 0
  foreach recipients [
   set recv turtle (read-from-string ?)
   if recv != nobody [without-interruption [ask recv [receive msg]]] ;; read-from-string is required to convert the string to number
  ]
end

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Message reception deals with updating incoming-queue
to receive [msg]
   if show_messages [show msg]
   set incoming-queue lput msg incoming-queue
end 

;; This reporter returns the next message in the list and removes it from the queue.
to-report get-message
  if empty? incoming-queue [report "no_message"]
  let nextmsg first incoming-queue
  remove-msg
  report nextmsg     
end 

;; This reporter returns the next message in the list WITHOUT removimg it from the queue.
to-report get-message-no-remove
  if empty? incoming-queue [report "no_message"]
  report first incoming-queue
end

;; Explicit remove-msg. 
;; This is needed since reporters *cannot* change a variable's values (apparently). 
to remove-msg
  set incoming-queue but-first incoming-queue
end 

;; broadcasting to all agents of breed t-breed
to broadcast-to [t-breed msg]
  foreach [who] of t-breed [
     send add-receiver ? msg    
  ]
end 


;; Creating Messages and adding the sender.
to-report create-message [performative]
 report (list performative (word "sender:" who) ) 
end 

to-report create-reply [performative msg]
let msgOut 0
    
 set msgOut create-message performative
 set msgOut add-receiver (get-sender msg) msgOut
 report msgOut
end 


;; Accesing information on Messages
;; Reports the sender of a message
to-report get-sender [msg]
  report remove "sender:" first (filter [not is-number? ? and member? "sender:" ?] msg) 
  ;;report item ((position "sender:" msg) + 1) msg 
end

;; Reports (returns) the content of a message
to-report get-content [msg]
  report item (position "content:" msg + 1) msg 
end

;; Reports the list of receivers of a message
to-report get-receivers [msg]
  report map [remove "receiver:" ?] filter [not is-number? ? and member? "receiver:" ?] msg
end 

;; reports the message performative. 
to-report get-performative [msg]
  report first msg
end 

;;; ADDING FIELDS TO A MESSAGE
;; Adding a sender to a message.
to-report add-sender [sender msg]
  report add msg "sender:" sender
end

;; add a receiver
to-report add-receiver [receiver msg]
  report add msg "receiver:" receiver
end

;; adding multiple recipients
to-report add-multiple-receivers [receivers msg]
  foreach receivers
  [
    set msg add-receiver ? msg
  ]
  report msg
end

;; Adding content to a message 
to-report add-content [content msg]
  report add msg "content:" content
end

;; Primitive Add command 
to-report add [msg field value]
  ifelse field = "content:"
  [report lput value lput field msg]
  [report lput (word field value) msg]
end  